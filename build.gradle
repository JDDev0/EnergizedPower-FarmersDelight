plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.

    maven { url "https://maven.shedaniel.me" }
    maven { url "https://modmaven.dev" }
    maven { url "https://maven.terraformersmc.com/" }
    maven {
        name = "Greenhouse Maven"
        url = 'https://maven.greenhouse.lgbt/releases/'
    }
    maven { url "https://mvn.devos.one/releases/" } // Porting Lib
    maven {
        url "https://maven.jamieswhiteshirt.com/libs-release" // Reach Entity Attributes (Required by Porting Lib)
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        url "https://jitpack.io/" // Fabric ASM
        content {
            excludeGroup "io.github.fabricators_of_create"
        }
    }

    maven {
        name "OctoStudios"
        url "https://maven.octo-studios.com/releases"
    }

    maven {
        url "https://maven.squiddev.cc/"
        content {
            includeGroup("cc.tweaked")
        }
    }

    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    flatDir {
        dir 'libs'
    }
}

fabricApi {
    configureDataGeneration {
        setClient(true)

        getOutputDirectory().set(getProject().file("src/generated/resources"))

        setModId(project.modid)
    }

    loom.runConfigs.datagen.runDir("run")
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Uncomment the following line to enable the deprecated Fabric API modules.
    // These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

    // modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"

    // Team Reborn: Fabric energy
    include modApi('teamreborn:energy:4.1.0') {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // Energized Power dependency
    modCompileOnly "curse.maven:energized-power-782147:7078194"

    modLocalRuntime "curse.maven:energized-power-782147:7078194"

    // Farmer's Delight Refabricated integration
    modCompileOnly "vectorwing:FarmersDelight:1.21.10-3.3.3+refabricated"

    modLocalRuntime "vectorwing:FarmersDelight:1.21.10-3.3.3+refabricated"

    // For more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
    //modLocalRuntime "dev.emi:emi-fabric:1.1.13+1.21.1"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

//Dummy task to fix post sync error caused by NeoGradle
tasks.register("idePostSync") {

}

tasks.register("distJar", Copy) {
    group = "build"

    dependsOn(tasks.getByName("remapJar"))

    var jarFile = project.rootDir.toPath().resolve("build/libs").resolve(project.archives_base_name + "-" + project.version + ".jar").toFile()
    from(jarFile)
    doFirst {
        file(project.rootDir.toPath().resolve("build/libs/dist")).mkdirs()
    }
    into(project.rootDir.toPath().resolve("build/libs/dist"))
}

tasks.publishUnified {
    dependsOn distJar
}

unifiedPublishing {
    project {
        displayName = "Energized Power - FD - " + project.minecraft_version + " - v" + project.mod_version.split("-")[1] + " [Fabric]"
        version = project.mod_version
        changelog = new File("CHANGELOG.md").exists()?new File("CHANGELOG.md").getText("UTF-8"):""
        releaseType = "release"
        gameVersions = [project.minecraft_version]
        gameLoaders = ["fabric"]

        mainPublication.set(project.rootDir.toPath().resolve("build/libs").resolve(project.archives_base_name + "-" + project.version + ".jar").toFile())

        relations {
            depends {
                modrinth = "fabric-api"
                curseforge = "fabric-api"
            }

            depends {
                modrinth = "energized-power"
                curseforge = "energized-power"
            }

            depends {
                modrinth = "farmers-delight-refabricated"
                curseforge = "farmers-delight-refabricated"
            }
        }

        if(project.hasProperty('MODRINTH_TOKEN')) {
            modrinth {
                token = getProperty('MODRINTH_TOKEN') as String
                id = "tfNnSImQ"
            }
        }

        if(project.hasProperty('CURSEFORGE_TOKEN')) {
            curseforge {
                token = getProperty('CURSEFORGE_TOKEN') as String
                id = "1373281"
            }
        }
    }
}
